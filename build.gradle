apply plugin: 'java'

import org.ajoberstar.grgit.*
import org.ajoberstar.grgit.util.*
import org.joda.time.*

//TODO:  Add support for WhisperSystems gradle-witness plugin for dependency verification
buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'org.ajoberstar:gradle-git:0.12.0'
        classpath 'joda-time:joda-time:2.0' // Sorry, not a fan of java.util.Date
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.1'
}

ext.repo = Grgit.open(project.file('.'))

jar {
    version = "master".equals(repo.branch.getCurrent().getName()) ?
            "${pullAncestorTag()}-${repo.head().abbreviatedId}" :
            "${pullAncestorTag()}-${DateTime.now()}"

    from(projectDir) {
        include 'installed.txt'
        include 'IntelliJ IDEA Global Settings'
        include 'colors/**'
        include 'options/**'
    }
}

/** Pull down the most recent ancestor tag (walking backwards in the tag hierarchy) and fetch the correct
 * version based on what source revision is currently checked out.
 *
 * @return a String containing the correct tag name of the current HEAD's ancestor
 */
def pullAncestorTag() {

    def value = repo.tag.list().reverse().find {
        JGitUtil.isAncestorOf(repo.repository, it.commit, repo.head())
    }

    return value.getName()
}
