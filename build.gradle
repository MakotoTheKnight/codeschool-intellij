apply plugin: 'java'

sourceCompatibility = 1.8

import org.ajoberstar.grgit.Grgit
import org.ajoberstar.grgit.util.JGitUtil
import java.time.LocalDateTime

ext {
    repo = Grgit.open()
}

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'org.ajoberstar:gradle-git:1.6.0'
    }
}

jar {
    version = "master".equals(repo.branch.getCurrent().getName()) ?
            "${pullAncestorTag()}-${repo.head().abbreviatedId}" :
            "${pullAncestorTag()}-${LocalDateTime.now()}"

    from(projectDir) {
        include 'installed.txt', 'IntelliJ IDEA Global Settings', 'colors/**', 'options/**'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.4.1'
}

/** Pull down the most recent ancestor tag (walking backwards in the tag hierarchy) and fetch the correct
 * version based on what source revision is currently checked out.
 *
 * @return a String containing the correct tag name of the current HEAD's ancestor
 */
def pullAncestorTag() {
    def value = repo.tag.list().reverse().find {
        JGitUtil.isAncestorOf(repo.repository, it.commit, repo.head())
    }
    return value.getName()
}
